WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ((
        "//" | "#" | ";")+ ~ (!NEWLINE ~ ANY)*
    ) | (
        "/*" ~ (!"*/" ~ ANY)* ~ "*/"
) }

character = @{ ASCII_ALPHANUMERIC | "_" }
word = @{ character+ }
number = @{ ASCII_DIGIT+ }
radix = @{ ("0x" ~ ASCII_HEX_DIGIT+) | ("0b" ~ ASCII_BIN_DIGIT+) }
negative = @{ "-" }
sep = _{ "," }

register = { "R" ~ number }
section = { "." ~ word }
label = { word ~ ":" }

op_2r = { "mov" | "inc" | "dec" | "shl" | "shr" | "ashl" | "ashr" | "not" | "ld" | "st" }
op_3r = { "add" | "sub" | "and" | "or" }
op_l = { "jmpz" | "jmps" | "jmpc" | "jmpnz" | "jmpns" | "jmpnc" | "jmp" }

instr_2r = {
    op_2r ~ register ~ sep? ~ register
}
instr_3r = {
    op_3r ~ register ~ sep? ~ register ~ sep? ~ register
}
instr_l = {
    op_l ~ word
}
instruction = { instr_2r | instr_3r | instr_l }

signed = { negative? ~ (radix | number) }
data = { signed ~ sep? }

line = { section | label | instruction | data+ }

file = {
    SOI ~
    ((section | label | instruction | data+)? ~ NEWLINE)* ~
    (section | label | instruction | data+)? ~
    EOI
}
